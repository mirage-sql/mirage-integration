plugins {
    id 'nebula.optional-base' version '3.2.0' // mvn style optional dependencies
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'

apply plugin: 'nebula.optional-base'

group 'com.miragesql'
version '2.1.0'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}


dependencies {
    compile 'com.miragesql:miragesql:2.1.0'
    compile 'com.miragesql:miragesql-test:2.1.0'

    compile 'org.slf4j:slf4j-api:1.7.25'

    compile 'commons-dbcp:commons-dbcp:1.4', optional
    compile 'com.zaxxer:HikariCP:2.7.2', optional

    compile "org.springframework:spring-core:3.2.18.RELEASE", optional
    compile "org.springframework:spring-context:3.2.18.RELEASE", optional
    compile "org.springframework:spring-jdbc:3.2.18.RELEASE", optional

    compile "com.google.inject:guice:4.1.0", optional


    compileOnly 'org.apache.geronimo.specs:geronimo-jta_1.1_spec:1.1.1'
    compileOnly 'javax.servlet:servlet-api:2.5'

    compile 'junit:junit:4.12'
    testImplementation 'cglib:cglib:3.2.5'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation 'com.h2database:h2:1.4.196'
    testImplementation 'org.hsqldb:hsqldb:2.4.0'

    testImplementation 'org.springframework:spring-aop:3.2.18.RELEASE'
}

// JavaDoc 8 problems
javadoc { failOnError false }

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion,
                'Target-JDK': project.targetCompatibility)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

def getPom(project) {
    return {
        name 'Mirage-SQL Integration'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'Mirage-SQL Integration is an easy way to integrate Mirage-SQL with Spring, Guice or Seasar2'
        url 'https://github.com/mirage-sql/mirage-integration'

        organization {
            name 'Mirage-SQL'
            url 'https://github.com/mirage-sql/'
        }

        scm {
            connection 'scm:git:https://github.com/mirage-sql/mirage-integration.git'
            developerConnection 'scm:git:https://github.com/mirage-sql/mirage-integration.git'
            url 'https://github.com/mirage-sql/mirage-integration'
        }

        issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/mirage-sql/mirage-integration/issues'
        }
        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'takezoe'
                name 'Naoki Takezoe'
                email 'takezoe_at_gmail.com'
                url 'http://d.hatena.ne.jp/takezoe/'
                timezone '+9'
            }
            developer {
                id 'daisuke'
                name 'Daisuke Miyamoto'
                email 'dai.0304_at_gmail.com'
                url 'http://d.hatena.ne.jp/daisuke-m/'
                timezone '+9'
            }
            developer {
                id 'aadrian'
                name 'Adrian A.'
                email 'a.adrian.tech_at_gmail.com'
                url 'https://github.com/aadrian'
                timezone '+1'
            }
        }
    }
}

// set these to 'dummy' for Travis CI
def stUser = hasProperty('ossrhUsername')? ossrhUsername: 'dummy'
def stPwd = hasProperty('ossrhPassword')? ossrhPassword: 'dummy'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: stUser, password: stPwd)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: stUser, password: stPwd)
            }

            pom {
                project getPom(project)
            }
        }
    }
}

// note: this task still does not display 'provided' and 'optional' correctly
task writeNewPom {
    doLast {
        pom {
            project getPom(project)
        }.writeTo("$buildDir/newpom.xml")
    }
}